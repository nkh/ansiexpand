#!/bin/env perl

use strict ; use warnings ; use Encode ;

my $ANSI = "(\e\[[0-9;]*m)" ;
my ($tab_size) = (8) ;

if(@ARGV and ($ARGV[0] eq '-t' or $ARGV[0] eq '--tabs'))
	{
	$tab_size = $ARGV[1] // 0 ;
	splice @ARGV, 0, 2 ;
	}

@ARGV = ('-') unless @ARGV ;

for(@ARGV)
	{
	my $fh ;
	
	if( $_ eq '-')
		{
		$fh = *STDIN ;
		}
	else
		{
		open $fh, '<', $_ or die "ansiexpand: can't open file '$_'\n" ;
		}
	
	while(my $string = <$fh>)
		{
		my ($expanded, $length) = ('', 0) ;
		
		for my $chunk ( split /$ANSI/, $string )
			{
			if($chunk =~ /$ANSI/)
				{
				$expanded .= $chunk ;
				}
			else
				{
				for my $schunk ( split /(\t)/, $chunk )
					{
					if($schunk eq "\t")
						{
						my $spaces = $tab_size - ($length % $tab_size) ;
						
						$expanded .= ' ' x $spaces ;
						$length += $spaces ;
						}
					else
						{
						my $u_schunk = decode('utf8', $schunk) ;
						
						$expanded .= $schunk ;
						$length += length($u_schunk) ;
						}
					}
				}
			}
		
		print $expanded ;
		}
	}

# vim: set ft=perl:
